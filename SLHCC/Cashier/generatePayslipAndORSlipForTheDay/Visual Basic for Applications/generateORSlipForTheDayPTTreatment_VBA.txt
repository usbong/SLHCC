Sub generateORSlipForTheDayPTTreatment()
' Copyright 2019 Usbong Social Systems, Inc.
'
' Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License. You ' may obtain a copy of the License at
'
' http://www.apache.org/licenses/LICENSE-2.0
'
' Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, ' WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language governing ' permissions and limitations under the License.
'
' @author: Michael Syson
' @date created: 20190509
' @date updated: 20190511
'
' Given:
' 1) Worksheet with the payslip data
'
'Output:
' 1) New workbook with auto-updated Worksheets for each transaction based on the OR template for PT Treatment
' 2) Automatically verified and corrected if necessary the computation in each of the worksheets with the transaction data in the new Workbook
'
Dim myInputWorkbook As Workbook 'added by Mike, 20190509
Dim myInput As Worksheet
Dim myOutput As Worksheet 'not the same as myInput
Dim row As Range
Dim RowCount As Integer
Dim TableHeaderRow As Integer
Dim LastPrintableRow As Integer
Dim MaxPatientNameColumnWidth As Integer
Dim patientNameFontSize As Integer
Dim particularsFontSize As Integer 'added by Mike, 20190323
Dim defaultFontName As String 'added by Mike, 20190314
Dim totalAmountReleasedValue As String 'added by Mike, 20190316

RowCount = 1
TableHeaderRow = 5 'output
LastPrintableRow = 16 'including the TOTAL PF
MaxPatientNameValueLength = 18 'characters in Pixels 'added by Mike, 20190323
MaxPatientNameColumnWidthPixels = 216 '12pt font * 18 characters in Pixels
patientNameFontSize = 12
MaxParticularsValueLength = 8 'characters in Pixels 'added by Mike, 20190323
MaxParticularsColumnWidthPixels = 96 '12pt font * 8 characters in Pixels 'added by Mike, 20190323
particularsFontSize = 12 'added by Mike, 20190323
defaultFontName = "Arial" 'added by Mike, 20190314
totalAmountReleasedValue = 0 'added by Mike, 20190316

Set myTemplate = Sheets("OR Template")
Set myInput = Sheets(ActiveSheet.Name)

'added by Mike, 20190314
Application.ScreenUpdating = False

'myTemplate.Copy ActiveWorkbook.Sheets(Sheets.Count)
'Set myOutput = Sheets(ActiveSheet.Name)
'myOutput.Name = Replace(myOutput.Name, "Template (2)", "") & myInput.Name

newWorkbookFileName = "OR Workbook " & myInput.Name

'added by Mike, 20190509
Set NewWorkbook = Workbooks.Add
    With NewWorkbook
        .SaveAs Filename:=newWorkbookFileName
    End With
    
'added by Mike, 20190509
'---------------------------------------------------------
'Auto-make worksheet based on OR template component part
'---------------------------------------------------------
RowCount = TableHeaderRow + 2 'input
OutputRowCountOffset = -1
For Each row In myInput.Rows
        myTemplate.Copy NewWorkbook.Sheets(Sheets.Count)
        Set myOutput = Sheets(ActiveSheet.Name)
        
        'added by Mike, 20190510
        'This resolves the issue with the image/picture logo not being in the correct scale.
        'This is when the template worksheet is copied to the new workbook.
        For Each p In ActiveSheet.Shapes
            'MsgBox ("There's a picture with the name: " & p.Name)
            p.ScaleHeight 1, msoScaleFromTopLeft
            p.ScaleWidth 0.96, msoScaleFromTopLeft
        Next p
        
        'added by Mike, 20190510; edited by Mike, 20190511
        'Write the value of the patient name from the input worksheet to the transaction's corresponding output worksheet
        myOutput.Range("C" & 9).Value = myInput.Range("C" & RowCount)
        
        'added by Mike, 20190510
        'Copy and paste the value of the Cashier person name from the input worksheet to the transaction's corresponding output worksheet
        myInput.Range("A" & 4).Columns.Copy
        myOutput.Range("I" & 47).PasteSpecial
        
        'added by Mike, 20190510; edited by Mike, 20190511
        'Write the current/present date from the input worksheet to the transaction's corresponding output worksheet
        myOutput.Range("J" & 7).Value = myInput.Range("A" & 3).Value
        
        'added by Mike, 20190511
        myOutput.Range("E" & 7).Value = Format(Now(), "yyyy-MM-ddTHH:mm:ss")
        
        'added by Mike, 20190511
        'Copy and paste the AMOUNT PAID from the input worksheet to the transaction's corresponding output worksheet
        If InStr(myInput.Range("D" & RowCount).Value, "SC") Or InStr(myInput.Range("D" & RowCount).Value, "PWD") Then
            myOutput.Range("E" & 18).Value = myInput.Range("E" & RowCount).Value
            myOutput.Range("D" & 18).Value = myOutput.Range("E" & 18).Value / 0.71428
        Else
            myOutput.Range("D" & 18).Value = myInput.Range("E" & RowCount)
        End If
        
        'We use the name of the patient as part of the worksheet name
        myOutput.Name = "OR PT " & myInput.Range("C" & RowCount).Value
      
        RowCount = RowCount + 1
                            
        If IsEmpty(myInput.Range("C" & RowCount).Value) Then
            Exit For
        End If
Next row

'added by Mike, 20190314
Application.ScreenUpdating = True

End Sub